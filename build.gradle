buildscript {
    ext {
        versionElasticsearch = "5.2.2"
    }
    repositories {
        maven { url 'http://dn6:8078' }
        mavenCentral()
        jcenter()
    }
}

allprojects {
    repositories {
        maven { url 'http://dn6:8078' }
        mavenCentral()
        jcenter()
    }

    // no unit tests
}

def ccCommonBuildScript = file("$rootDir/gradle/common.gradle")
def ccLibs = fileTree(dir: "$rootDir/lib", include: '*.jar', excludes: ["*-sources.jar", "*-src.jar"])

project(':common') {
    apply from: ccCommonBuildScript

    description = 'common'

    dependencies {
        compile ccLibs
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    }
}

project(':hanlp-io') {
    apply from: ccCommonBuildScript

    description = 'hanlp-io'

    dependencies {
        compile project(':common')

        compile("org.apache.hadoop:hadoop-client:2.7.3")
    }
}

project(':es-plugin') {
    apply from: ccCommonBuildScript

    description = 'es-plugin'

    version = versionElasticsearch

    configurations {
        includeJars
    }

    dependencies {
        compile project(':common')

        compile("com.hankcs.nlp:hanlp-lucene-plugin:1.1.2") {
            exclude group: "com.hankcs", module: "hanlp"
        }
//        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8'
        compile("org.elasticsearch:elasticsearch:$versionElasticsearch")
        testCompile("org.elasticsearch.test:framework:$versionElasticsearch")

        includeJars(ccLibs)
        includeJars("com.hankcs.nlp:hanlp-lucene-plugin:1.1.2") {
            exclude group: "com.hankcs", module: "hanlp"
            exclude group: "org.apache.lucene"
        }
        includeJars(project(':common'))
    }

    test.enabled = false

    task buildPluginZip(type: Zip) {
        baseName = 'elasticsearch-analysis-hanlp'
        from files(libsDir)
        from 'src/main/universal'
        from configurations.includeJars.collect { it }
        into 'elasticsearch'
    }

    artifacts {
        archives buildPluginZip
    }
}

